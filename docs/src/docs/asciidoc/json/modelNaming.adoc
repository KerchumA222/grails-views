Model names can be specified in a couple different ways.

NOTE: Some model names are _reserved_ since there are properties of the same name injected into the view:
`locale`, `response`, `request`, `page`, `controllerNamespace`, `controllerName`, `actionName`, `config`

==== Models referenced explicitly by name

Given a view:
[source,groovy]
.`grails-app/views/hello/index.gson`
model {
    String message
}
json.message {
    hello message
}

Then the controller has to specify the model name by passing a map to `respond`:

[source,groovy]
.`grails-app/controllers/helloController.groovy`
def index() {
    respond(message: "Hello World")
}

When using a template:

[source,groovy]
.`grails-app/views/hello/_msg.gson`
model {
    String message
}
json.message {
    hello message
}

In the view you can use the `tmpl` namespace:
[source,groovy]
json {
    message tmpl.msg(message)
}

or you can use `g.render`

[source,groovy]
json {
    message g.render(template:'msg', model:[message: message])
}

or

[source,groovy]
json {
    message g.render(template:'msg', model: message, var:'message')
}

Using collections:

[source,groovy]
model {
    List<String> messages
}
json {
    messages tmpl.msg('message', messages)
}

or

[source,groovy]
json {
    messages g.render(template:'msg', collection: messages, var: 'message')
}

==== Model name by convention

Model names are, by convention, based on the type.

Given a view:
[source,groovy]
.`grails-app/views/hello/index.gson`
model {
    String string
}
json.message {
    hello string
}

You can pass in the variable directly to `respond`:

[source,groovy]
.`grails-app/controllers/helloController.groovy`
def index() {
    respond("Hello World")
}


When using a template the model name is based on the template name:

In the view you can use the `tmpl` namespace:
[source,groovy]
.`grails-app/views/hello/index.gson`
json.message tmpl.msg(message)

[source,groovy]
.`grails-app/views/hello/_msg.gson`
model {
    String msg
}
json.message {
    hello msg
}

When using `g.render`, the model name must always be specified by using a map.

[source,groovy]
json{
    message g.render(template:'msg', model: [msg: string])
}

Using collections:
[source,groovy]
model {
    List<String> stringList
}
json {
    messages tmpl.msg(stringList)
}

When using g.render, you *must* set the `var` parameter to the variable name in the template.
[source,groovy]
json {
    messages g.render(template:'msg', collection: stringList, var: 'string')
}